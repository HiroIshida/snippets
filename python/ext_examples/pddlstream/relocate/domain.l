(define (domain reach)
  (:requirements :strips :equality)
  (:predicates
    ;; types
    (TypeConf ?q)
    (TypePose ?pose)
    (TypeGrasp ?grasp)
    ;; fluents
    (AtConf ?q)
    (AtPose ?object ?pose)
    (AtGrasp ?object ?grasp)
    (IsHandEmpty)
    (IsHolding ?object)
    (IsUnderRelocation ?object)
    ;; others
    (KinGrasp ?object ?pose ?grasp ?q)
    (IsCFreePathFromHome ?traj ?q)
    (IsCFreePathGrasping ?object ?grasp ?q1 ?traj ?q2)
    (IsGraspable ?object)
    (IsGrasp ?grasp ?object)
  )
  (:action hand_free_move
    :parameters (?q1 ?traj ?q2)
    :precondition (and
        (AtConf ?q1)
        (IsCFreePathFromHome ?traj ?q2)
    )
    :effect (and
        (not (AtConf ?q1))
        (AtConf ?q2)
    )
  )
  (:action grasp
     :parameters (?object ?pose ?grasp ?q)
     :precondition (and
        (IsGraspable ?object)
        (AtPose ?object ?pose)
        (AtConf ?q)
        (IsHandEmpty)
        (not (IsUnderRelocation ?object))
        (IsGrasp ?grasp ?object)
        (KinGrasp ?object ?pose ?grasp ?q)
     )
     :effect (and
        (not (IsHandEmpty))
        (IsHolding ?object)
        (not (AtPose ?object ?pose))
        (AtGrasp ?object ?grasp)
     )
  )
  (:action release
    :parameters (?object ?pose ?grasp ?q)
    :precondition (and
        (AtConf ?q)
        (IsHolding ?object)
        (AtGrasp ?object ?grasp)
        (IsUnderRelocation ?object)
        (KinGrasp ?object ?pose ?grasp ?q)
     )
     :effect (and
        (not (IsHolding ?object))
        (IsHandEmpty)
        (not (AtGrasp ?object ?grasp))
        (AtPose ?object ?pose)
        (not (IsUnderRelocation ?object))
     )
  )
  (:action relocate
    :parameters (?object ?pose1 ?grasp ?q1 ?traj ?pose2 ?q2)
    :precondition (and
        (AtConf ?q1)
        (IsHolding ?object)
        (AtGrasp ?object ?grasp)
        (IsCFreePathGrasping ?object ?grasp ?q1 ?traj ?q2)
        (KinGrasp ?object ?pose1 ?grasp ?q1)
        (KinGrasp ?object ?pose2 ?grasp ?q2)
        (not (= ?pose1 ?pose2))
    )
    :effect (and
        (not (AtConf ?q1))
        (AtConf ?q2)
        (IsUnderRelocation ?object)
    )
  )
)
